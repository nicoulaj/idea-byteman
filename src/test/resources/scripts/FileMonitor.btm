########################################################################
# JBoss, Home of Professional Open Source
# Copyright 2009-2010, Red Hat and individual contributors
# by the @authors tag. See the copyright.txt in the distribution for a
# full listing of individual contributors.
#
# This is free software; you can redistribute it and/or modify it
# under the terms of the GNU Lesser General Public License as
# published by the Free Software Foundation; either version 2.1 of
# the License, or (at your option) any later version.
#
# This software is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public
# License along with this software; if not, write to the Free
# Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA
# 02110-1301 USA, or see the FSF site: http://www.fsf.org.
#
# @authors Andrew Dinn
#
# FileMonitor
#
# A byteman script which prints a stack trace whenever a file input or
# output stream is created from a File instance or subsequently closed.
#
# to use ths script to trace execution of java program org.my.App execute
#
#  -- set the directory in which byteman has been installed
#  BYTEMAN_HOME= ...
#
#   -- identify this script
#   SCRIPT={BYTEMAN_HOME}/sample/scripts/FileMonitor.txt
#
#  ${BYTEMAN_HOME}/bin/bmjava.sh -l $SCRIPT org.my.App
#
# alternatively to load the script dynamically
#
#   -- start the program with the agent
#  ${BYTEMAN_HOME}/bin/bmjava.sh org.my.App
#
#   -- install the script
#  ${BYTEMAN_HOME}/bin/bmsubmit.sh -l $SCRIPT

########################################################################
#
# Rule to trace create of file input stream from File
#

RULE FileMonitor trace FileInputStream create from File
CLASS java.io.FileInputStream
METHOD <init>(java.io.File)
AT RETURN
IF TRUE
DO traceStack("*** Opened " + $1.getPath() + " for read in thread " + Thread.currentThread().getName() + "\n", 10)
ENDRULE

########################################################################
#
# Rule to trace close of file input stream close
#

RULE FileMonitor trace FileInputStream close
CLASS java.io.FileInputStream
METHOD close
AT RETURN
IF TRUE
DO traceStack("*** Closed " + $0 + " in thread " + Thread.currentThread().getName() + "\n", 10)
ENDRULE

########################################################################
#
# Rule to trace create of file output stream from File
#

RULE FileMonitor trace FileOutputStream create from File
CLASS java.io.FileOutputStream
METHOD <init>(java.io.File, boolean)
AT RETURN
IF TRUE
      DO traceStack("*** Opened " + $1.getPath() + " for " + ($2 ? "write" : "append") +" in thread " + Thread.currentThread().getName() + "\n", 10)
ENDRULE

########################################################################
#
# Rule to trace close of file output
#

RULE FileMonitor trace FileOutputStream close
CLASS java.io.FileOutputStream
METHOD close()
AT RETURN
IF TRUE
DO traceStack("*** Closed " + $0 + " in thread " + Thread.currentThread().getName() + "\n", 10)
ENDRULE
